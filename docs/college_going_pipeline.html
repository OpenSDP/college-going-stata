<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>college_going_pipeline</title>
  <style type="text/css">
      code{white-space: pre-wrap;}
      span.smallcaps{font-variant: small-caps;}
      span.underline{text-decoration: underline;}
      div.column{display: inline-block; vertical-align: top; width: 50%;}
  </style>
  <!--[if lt IE 9]>
    <script src="//cdnjs.cloudflare.com/ajax/libs/html5shiv/3.7.3/html5shiv-printshiv.min.js"></script>
  <![endif]-->
</head>
<body>
&lt;!DOCTYPE html&gt;
<html lang="en">
<head>
<meta charset="utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="viewport" content="width=device-width, initial-scale=1">
<meta name="format-detection" content="telephone=no">
<title>
college_going_pipeline.md
</title>
<link href="https://maxcdn.bootstrapcdn.com/bootswatch/3.3.7/simplex/bootstrap.min.css" rel="stylesheet" integrity="sha384-C0X5qw1DlkeV0RDunhmi4cUBUkPDTvUqzElcNWm1NI2T4k8tKMZ+wRPQOhZfSJ9N" crossorigin="anonymous">
<style>
img { max-width: 100%; height: auto; }
pre { word-break: normal; word-wrap: normal; }
code { color: inherit; background-color: #F5F5F5; }
pre code, pre samp { white-space: pre; }
</style>
<style>
.stcmd .stcmt { font-style: italic; opacity: 0.5; }
.stoom, .stcnp { font-style: italic; }
@media screen { .stcnp { display: none; }}
</style>
</head>
<body>
<style>
.affix {
   position: static;
 }
 @media (min-width: 992px) {
    #toc.affix {
        top: inherit;
        width: inherit;
        position: fixed;
    }
}

a {
  color: #A51C30;
}

a:focus, a:hover {
  color: #6D121F;
}

/* Fix for anchor link scroll location */
h1:before, h2:before, h3:before, h4:before {
  display: block;
  content: " ";
  margin-top: -124px;
  height: 124px;
  visibility: hidden;
}

#logo{
     background-color: #A51C30;
}
body {
    padding-top: 115px;
    overflow: visible;
}
html {
    overflow: visible;
}
</style>
<!-- Global site tag (gtag.js) - Google Analytics -->
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-129904036-1"></script>
<script>
  window.dataLayer = window.dataLayer || [];
  function gtag(){dataLayer.push(arguments);}
  gtag('js', new Date());
 
  gtag('config', 'UA-129904036-1');
</script>
<div id="logo" class="navbar navbar-default navbar-fixed-top">
<div class="container">
<p><img src="https://opensdp.github.io/assets/images/OpenSDP-Banner_crimson.jpg" style="display: block; margin: 0 auto; height: 115px;"></p>
</div>
</div>
<pre id="stlog-1" class="stcmd"><code><span class="stcmt">// Modify here, bug causes nav to spill behind body on ultra large resolutions</span>
</code></pre>
<div class="container">
<div class="row">
<nav class="col-xs-12 col-sm-12 col-md-3" style = "padding-right:15px;">
<div id="toc" class="nav nav-stacked affix" style="overflow-wrap:break-word;">
<div class="well" style="padding-right:10px; overflow-wrap:break-word;">
<ul>
<li>
<a href="#h-1">Attainment Along the Education Pipeline</a>
<ul>
<li>
<a href="#h-1-1">Getting Started</a>
<ul>
<li>
<a href="#h-1-1-1">Objective</a>
</li>
<li>
<a href="#h-1-1-2">Using this Guide</a>
</li>
<li>
<a href="#h-1-1-3">About the Data</a>
</li>
<li>
<a href="#h-1-1-4">About the Analyses</a>
</li>
<li>
<a href="#h-1-1-5">Sample Restrictions</a>
</li>
<li>
<a href="#h-1-1-6">Giving Feedback on this Guide</a>
</li>
</ul>
</li>
<li>
<a href="#h-1-2">Analyses</a>
<ul>
<li>
<a href="#h-1-2-1">Overall Progression</a>
</li>
<li>
<a href="#h-1-2-2">Progression by Student Race/Ethnicity</a>
</li>
<li>
<a href="#h-1-2-3">Progression by Student Race/Ethnicity, Among FRPL Students</a>
</li>
<li>
<a href="#h-1-2-4">Progression by Students’ On-Track Status After Ninth Grade</a>
</li>
</ul>
</li>
</ul>
</li>
</ul>
</div>
</div>
</div>
<div class="col-xs-12 col-sm-12 col-md-8 offset-md-1 pull-right">
<h1 id="h-1">
Attainment Along the Education Pipeline
</h1>
<h4 id="college-going-pathways"><em>College-Going Pathways</em></h4>
<h4 id="stata-version"><em>Stata Version</em></h4>
<h2 id="h-1-1">
Getting Started
</h2>
<h3 id="h-1-1-1">
Objective
</h3>
<p>In this guide you will be able to visualize the progress of students and student subgroups through important milestones from ninth grade through the second year of college</p>
<h3 id="h-1-1-2">
Using this Guide
</h3>
<p>The College-Going Pathways series is a set of guides, code, and sample data about policy-relevant college-going topics. Browse this and other guides in the series for ideas about ways to investigate student pathways through high school and college. Each guide includes several analyses in the form of charts together with Stata analysis and graphing code to generate each chart.</p>
<p>Once you’ve identified analyses that you want to try to replicate or modify, click the “Download” buttons to download Stata code and sample data. You can make changes to the charts using the code and sample data, or modify the code to work with your own data. If you’re familiar with Github, you can click “Go to Repository” and clone the entire College-Going Pathways repository to your own computer.</p>
<h3 id="h-1-1-3">
About the Data
</h3>
<p>The data visualizations in the College-Going Pathways series use a synthetically generated college-going analysis sample data file which has one record per student. Each high school student is assigned to a ninth-grade cohort, and each student record includes demographic and program participation information, annual GPA and on-track status, high school graduation outcomes, and college enrollment information. The Connect guide (coming soon) will provide guidance and example code which will help you build a college-going analysis file using data from your own school system.</p>
<h3 id="h-1-1-4">
About the Analyses
</h3>
<p>The analyses in this guide summarize student attainment from ninth grade through college using three milestones: 1) on-time high school completion, 2) seamless college transition, and 3) persistence to the second year of college.</p>
<p>Through these analyses, you identify drop-offs along the education pipeline for students as a group and as subgroups. For different subgroups, these analyses illuminate disparities in college attainment by race, family income, high school attended, and academic achievement. A steep decline in college enrollment from high school completion date for specific subgroups may indicate barriers to college access. On the other hand, a steep decline from initial college enrollment to second-year persistence might suggest students were not prepared for rigorous college coursework during high school.</p>
<h3 id="h-1-1-5">
Sample Restrictions
</h3>
<p>One of the most important decisions in running each analysis is defining the sample. Each analysis corresponds to a different part of the education pipeline and as a result requires different cohorts of students.</p>
<p>If you are using the synthetic data we have provided, the sample restrictions have been predefined and are included below. If you run this code using your own agency data, change the sample restrictions based on your data. Note that you will have to run these sample restrictions at the beginning of your do file so they will feed into the rest of your code.</p>
<pre id="stlog-2" class="stcmd"><code>
global agency_name "Agency"

<span class="stcmt">// Sample Restrictions</span>

<span class="stcmt">// Ninth grade cohorts you can observe persisting to the second year of college</span>
global chrt_ninth_begin_persist_yr2 = 2004
global chrt_ninth_end_persist_yr2 = 2006

<span class="stcmt">// Ninth grade cohorts you can observe graduating high school on time</span>
global chrt_ninth_begin_grad = 2004
global chrt_ninth_end_grad = 2006

<span class="stcmt">// Ninth grade cohorts you can observe graduating high school one year late</span>
global chrt_ninth_begin_grad_late = 2004
global chrt_ninth_end_grad_late = 2006

<span class="stcmt">// High school graduation cohorts you can observe enrolling in college the fall after graduation</span>
global chrt_grad_begin = 2007
global chrt_grad_end = 2009

<span class="stcmt">// High school graduation cohorts you can observe enrolling in college two years after hs graduation</span>
global chrt_grad_begin_delayed = 2007
global chrt_grad_end_delayed = 2009

</code></pre>
<p>Based on the sample data, you will have three cohorts (sometimes only two) for analysis. If you are using your own agency data, you may decide to aggregate results for more or fewer cohorts to report your results. This decision depends on 1) how much historical data you have available and 2) what balance to strike between reliability and averaging away information on recent trends. We suggest you average results for the last three cohorts to take advantage of larger sample sizes and improve reliability. However, if you have data for more than three cohorts, you may decide to not average data out for fear of losing information about trends and recent changes in your agency.</p>
<h3 id="h-1-1-6">
Giving Feedback on this Guide
</h3>
<p>This guide is an open-source document hosted on Github and generated using the Stata Webdoc package. We welcome feedback, corrections, additions, and updates. Please visit the OpenSDP college-going pathways repository to read our contributor guidelines.</p>
<h2 id="h-1-2">
Analyses
</h2>
<h3 id="h-1-2-1">
Overall Progression
</h3>
<p><strong>Purpose:</strong> This analysis tracks the overall percent of ninth graders who complete high school on-time, seamlessly enroll in college, and persist to the second year of college. To examine the range of attainment at each milestone, the minimum and maximum values of any high school are shown.</p>
<p><strong>Required Analysis File Variables:</strong></p>
<ul>
<li><code>sid</code></li>
<li><code>chrt_ninth</code></li>
<li><code>first_hs_name</code></li>
<li><code>ontime_grad</code></li>
<li><code>enrl_1oct_ninth_yr1_any</code></li>
<li><code>enrl_1oct_ninth_yr2_any</code></li>
</ul>
<p><strong>Analysis-Specific Sample Restrictions:</strong> Keep students in ninth grade cohorts for which persistence to the second year of college can be reported.</p>
<p><strong>Ask Yourself</strong></p>
<ul>
<li>Do you notice drop-offs along the pipeline?</li>
<li>Are differences in agency maxima and minima at different points along the pipeline surprising? What might be different about these high schools?</li>
<li>Are your numbers in line with agency-reported figures in other publicly available reports? What might account for differences?</li>
</ul>
<p><strong>Analytic Technique:</strong> Calculate the proportion of first-time ninth graders that progress to each step along the education pipeline.</p>
<figure id="fig-3">
<a href="pipeline_img/3.png"><img alt="pipeline_img/3.png" src="pipeline_img/3.png"/></a>
</figure>
<pre id="stlog-3" class="stcmd"><code>
<span class="stcmt">// Overall Progression</span>

<span class="stcmt">// Step 1: Load the college-going analysis file into Stata</span>
use "$data/college_going_analysis", clear

<span class="stcmt">// Step 2: Keep students in ninth grade cohorts you can observe persisting to the second year of college</span>
local chrt_ninth_begin = ${chrt_ninth_begin_persist_yr2}
local chrt_ninth_end = ${chrt_ninth_end_persist_yr2}
keep if (chrt_ninth &gt;= `chrt_ninth_begin' &amp; chrt_ninth &lt;= `chrt_ninth_end')

<span class="stcmt">// Step 3: Create variables for the outcomes "regular diploma recipients", "seamless transitioners" and "second year persisters"</span>
gen grad = (!mi(chrt_grad) &amp; ontime_grad == 1)
gen seamless_transitioners_any = (enrl_1oct_ninth_yr1_any == 1 &amp; ontime_grad == 1)
gen second_year_persisters = (enrl_1oct_ninth_yr1_any == 1 &amp; enrl_1oct_ninth_yr2_any == 1 &amp; ontime_grad == 1)

<span class="stcmt">// Step 4: Create agency-level average outcomes</span>
<span class="stcmt">// 1. Preserve the data (to work with the data in its existing structure later on)</span>
preserve

<span class="stcmt">// 2. Calculate the mean of each outcome variable by agency</span>
    collapse (mean) grad seamless_transitioners_any second_year_persisters (count) N = sid

<span class="stcmt">// 3. Create a string variable called school_name equal to "${agency_name} Average"</span>
    gen school_name = "${agency_name} AVERAGE"

<span class="stcmt">// 4. Save this data as a temporary file</span>
    tempfile agency_level
    save `agency_level'

<span class="stcmt">// 5. Restore the data to the original form</span>
restore

<span class="stcmt">// Step 5: Create school-level maximum and minimum outcomes</span>
<span class="stcmt">// 1. Create a variable school_name that takes on the value of studentsҠfirst high school attended</span>
gen school_name = first_hs_name
<span class="stcmt">// 2. Calculate the mean of each outcome variable by first high school attended</span>
collapse (mean) grad seamless_transitioners second_year_persisters (count) N = sid, by(school_name)

<span class="stcmt">// 3. Identify the agency maximum values for each of the three outcome variables</span>
preserve
    collapse (max) grad seamless_transitioners_any second_year_persisters (count) N
    gen school_name = "${agency_name} MAX HS"

    tempfile agency_max
    save `agency_max'
restore

<span class="stcmt">// 4. Identify the agency minimum values for each of the three outcome variables</span>
preserve
    collapse (min) grad seamless_transitioners_any second_year_persisters (count) N
    gen school_name = "${agency_name} MIN HS"

    tempfile agency_min
    save `agency_min'
restore

<span class="stcmt">// 5. Append the three tempfiles to the school-level file loaded into Stata</span>
append using `agency_level'
append using `agency_max'
append using `agency_min'

<span class="stcmt">// Step 6: Format the outcome variables so they read as percentages in the graph</span>
foreach var of varlist grad seamless_transitioners_any second_year_persisters {
    replace `var' = (`var' * 100)
    format `var' %9.1f
}

<span class="stcmt">// Step 7: Reformat the data file so that one variable contains all the outcomes of interest</span>
<span class="stcmt">// 1. Create 4 observations for each school: ninth grade, hs graduation, seamless college transition and second-year persistence</span>
foreach i of numlist 1/4 {
    gen time`i' = `i'
}

<span class="stcmt">// 2. Reshape the data file from wide to long</span>
reshape long time , i(school_name N)
drop _j

<span class="stcmt">// 3. Create a single variable that takes on all the outcomes of interest</span>
bysort school_name: gen outcome = 100 if time == 1
bysort school_name: replace outcome = grad if time == 2
bysort school_name: replace outcome = seamless_transitioners_any if time == 3
bysort school_name: replace outcome = second_year_persisters if time == 4
format outcome %9.1f

<span class="stcmt">// Step 8: Prepare to graph the results</span>
<span class="stcmt">// 1. Label the outcome</span>
label define outcome 1 "Ninth Graders" 2 "On-time Graduates" <span class="stcmt">///</span>
3 "Seamless College Transitioners" 4 "Second Year Persisters"
label values time outcome

<span class="stcmt">// 2. Generate a cohort label to be used in the footnote for the graph</span>
local temp_begin = `chrt_ninth_begin'-1
local temp_end = `chrt_ninth_end'-1
if `chrt_ninth_begin'==`chrt_ninth_end' {
        local chrt_label "`temp_begin'-`chrt_ninth_begin'"
}
else {
        local chrt_label "`temp_begin'-`chrt_ninth_begin' through `temp_end'-`chrt_ninth_end'"
}

<span class="stcmt">// Step 9: Graph the results</span>
#delimit ;
twoway (connected outcome time if school_name == "${agency_name} AVERAGE",
        sort lcolor(dkorange) mlabel(outcome) mlabc(black) mlabs(vsmall) mlabp(12)
        mcolor(dknavy) msymbol(circle) msize(small))
        (connected outcome time if school_name == "${agency_name} MAX HS", sort lcolor(black)
        lpattern(dash) mlabel(outcome) mlabs(vsmall) mlabp(12) mlabc(black)
        mcolor(black) msize(small))
        (connected outcome time if school_name == "${agency_name} MIN HS", sort lcolor(blue)
        lpattern(dash) mlabel(outcome) mlabs(vsmall) mlabp(12) mlabc(black)
        mcolor(black) msize(small)),
title("Student Progression from Ninth Grade Through College", size(medium))
        subtitle("${agency_name} Average", size(medsmall))
        xscale(range(.8(.2)4.2))
        xtitle("") xlabel(1 2 3 4 , valuelabels labsize(vsmall))
        ytitle("Percent of Ninth Graders")
        yscale(range(0(20)100))
        ylabel(0(20)100, nogrid)
legend(col(1) position(2) size(vsmall)
        label(1 "${agency_name} Average")
        label(2 "${agency_name} Max HS")
        label(3 "${agency_name} Min HS")
        ring(0) region(lpattern(none) lcolor(none) fcolor(none)))
graphregion(color(white) fcolor(white) lcolor(white))
plotregion(color(white) fcolor(white) lcolor(white))
note(" " "Sample: `chrt_label' ${agency_name} first-time ninth graders." "Postsecondary enrollment outcomes from NSC matched records. All other data from ${agency_name} administrative records.", size(vsmall));
#delimit cr

graph export "$figures/A1_Overall_Progression.png", replace width(1600) height(1200)
</code></pre>
<h3 id="h-1-2-2">
Progression by Student Race/Ethnicity
</h3>
<p><strong>Purpose:</strong> This analysis tracks the percent of ninth graders of different races/ethnicities who complete high school on-time, seamlessly enroll in college, and persist to the second year of college.</p>
<p><strong>Required Analysis File Variables:</strong></p>
<ul>
<li><code>sid</code></li>
<li><code>race_ethnicity</code></li>
<li><code>chrt_ninth</code></li>
<li><code>ontime_grad</code></li>
<li><code>enrl_1oct_ninth_yr1_any</code></li>
<li><code>enrl_1oct_ninth_yr2_any</code></li>
</ul>
<p><strong>Analysis-Specific Sample Restrictions:</strong></p>
<ul>
<li>Keep students in ninth grade cohorts for which persistence to the second year of college can be reported.</li>
<li>Restrict the sample to include students from the most representative racial/ethnic sub-groups.</li>
</ul>
<p><strong>Ask Yourself</strong></p>
<ul>
<li>Which races/ethnicities face larger drop-offs along the pipeline?</li>
<li>Might certain groups face different barriers to progressing along the education pipeline?</li>
</ul>
<p><strong>Analytic Technique:</strong> Calculate the proportion of first-time ninth graders that progress to each step along the education pipeline.</p>
<figure id="fig-4">
<a href="pipeline_img/4.png"><img alt="pipeline_img/4.png" src="pipeline_img/4.png"/></a>
</figure>
<pre id="stlog-4" class="stcmd"><code>
<span class="stcmt">// Progression by Student Race/Ethnicity</span>

<span class="stcmt">// Step 1: Load the college-going analysis file into Stata</span>
use "$data/college_going_analysis", clear

<span class="stcmt">// Step 2: Keep students in ninth grade cohorts you can observe persisting to the second year of college</span>
local chrt_ninth_begin = ${chrt_ninth_begin_persist_yr2}
local chrt_ninth_end = ${chrt_ninth_end_persist_yr2}
keep if (chrt_ninth &gt;= `chrt_ninth_begin' &amp; chrt_ninth &lt;= `chrt_ninth_end')

<span class="stcmt">// Step 3: Create variables for the outcomes "regular diploma recipients", "seamless transitioners" and "second year persisters"</span>
gen grad = (!mi(chrt_grad) &amp; ontime_grad == 1)
gen seamless_transitioners_any = (enrl_1oct_ninth_yr1_any == 1 &amp; ontime_grad == 1)
gen second_year_persisters = (enrl_1oct_ninth_yr1_any == 1 &amp; enrl_1oct_ninth_yr2_any == 1 &amp; ontime_grad == 1)

<span class="stcmt">// Step 4: Create average outcomes by race/ethnicity</span>
collapse (mean) grad seamless_transitioners_any second_year_persisters (count) N=sid, <span class="stcmt">///</span>
by(race_ethnicity)

<span class="stcmt">// Step 5: Format the outcome variables so they read as percentages in the graph</span>
foreach var of varlist grad seamless_transitioners_any second_year_persisters {
    replace `var' = (`var' * 100)
    format `var' %9.1f
}

<span class="stcmt">// Step 6: Reformat the data file so that one variable contains all the outcomes of interest</span>
<span class="stcmt">// 1. Create 4 observations for each school: ninth grade, hs graduation, seamless college transition and second-year persistence</span>
foreach i of numlist 1/4 {
    gen time`i' = `i'
}

<span class="stcmt">// 2. Keep only African-American, Asian-American, Hispanic, and White students</span>
keep if race_ethnicity == 1 | race_ethnicity == 2 | race_ethnicity == 3 | race_ethnicity == 5
sort race_ethnicity
gen sortorder = _n

<span class="stcmt">// 3. Reshape the data file from wide to long</span>
reshape long time , i(sortorder)

<span class="stcmt">// 4. Create a single variable that takes on all the outcomes of interest</span>
bysort race_ethnicity: gen outcome = 100 if time == 1
bysort race_ethnicity: replace outcome = grad if time == 2
bysort race_ethnicity: replace outcome = seamless_transitioners_any if time == 3
bysort race_ethnicity: replace outcome = second_year_persisters if time == 4
format outcome %9.1f

<span class="stcmt">// Step 7: Prepare to graph the results</span>
<span class="stcmt">// 1. Label the outcome</span>
label define outcome 1 "Ninth Graders" 2 "On-time Graduates" <span class="stcmt">///</span>
3 "Seamless College Transitioners" 4 "Second Year Persisters"
label values time outcome

<span class="stcmt">// 2. Generate a cohort label to be used in the footnote for the graph</span>
local temp_begin = `chrt_ninth_begin'-1
local temp_end = `chrt_ninth_end'-1
if `chrt_ninth_begin'==`chrt_ninth_end' {
        local chrt_label "`temp_begin'-`chrt_ninth_begin'"
}
else {
        local chrt_label "`temp_begin'-`chrt_ninth_begin' through `temp_end'-`chrt_ninth_end'"
}

<span class="stcmt">// Step 8: Graph the results</span>
#delimit;
twoway (connected outcome time if race_ethnicity==1,
        sort lcolor(dknavy) mlabel(outcome) mlabc(black)mlabs(vsmall) mlabp(12)
        mcolor(dknavy) msymbol(circle) msize(small))
        (connected outcome time if race_ethnicity==2 , sort lcolor(lavender) lpattern(dash)
        mlabel(outcome) mlabs(vsmall) mlabp(12) mlabc(black) mcolor(lavender) msize(small))
        (connected outcome time if race_ethnicity==3 , sort lcolor(dkgreen) lpattern(dash)
        mlabel(outcome) mlabs(vsmall) mlabp(12) mlabc(black) mcolor(dkgreen) msize(small))
        (connected outcome time if race_ethnicity==5 , sort lcolor(orange) mlabel(outcome) mlabc(black)
        mlabs(vsmall) mlabp(12) mcolor(orange) msymbol(circle) msize(small)),
title("Student Progression from Ninth Grade Through College", size(medium))
        subtitle("By Student Race/Ethnicity", size(medsmall))
        xscale(range(.8(.2)4.2))
        xlabel(1 2 3 4 , valuelabels labsize(vsmall))
        ytitle("Percent of Ninth Graders")
        yscale(range(0(20)100))
        ylabel(0(20)100, nogrid)
        xtitle("", color(white))
legend(order(2 4 1 3) col(1) position(2) size(vsmall)
        label(1 "African American Students")
        label(2 "Asian American Students")
        label(3 "Hispanic Students")
        label(4 "White Students")
        ring(0) region(lpattern(none) lcolor(none) fcolor(none)))
graphregion(color(white) fcolor(white) lcolor(white))
plotregion(color(white) fcolor(white) lcolor(white))
note(" " "Sample: `chrt_label' ${agency_name} first-time ninth graders." "Postsecondary enrollment outcomes from NSC matched records. All other data from ${agency_name} administrative records." , size(vsmall));
#delimit cr

graph export "$figures/A2_Progression_by_RaceEthnicity.png", replace width(1600) height(1200)
</code></pre>
<h3 id="h-1-2-3">
Progression by Student Race/Ethnicity, Among FRPL Students
</h3>
<p><strong>Purpose:</strong> This analysis tracks the percent of ninth graders of different races/ethnicities who ever qualified for free or reduce price lunch who complete high school on-time, seamlessly enroll in college, and persist to the second year of college.</p>
<p><strong>Required Analysis File Variables:</strong></p>
<ul>
<li><code>sid</code></li>
<li><code>race_ethnicity</code></li>
<li><code>frpl_ever</code></li>
<li><code>chrt_ninth</code></li>
<li><code>ontime_grad</code></li>
<li><code>enrl_1oct_ninth_yr1_any</code></li>
<li><code>enrl_1oct_ninth_yr2_any</code></li>
</ul>
<p><strong>Analysis-Specific Sample Restrictions:</strong></p>
<ul>
<li>Keep students in ninth grade cohorts for which persistence to the second year of college can be reported.</li>
<li>Restrict the analysis to include only students who were ever eligible to receive free-or reduced-price lunch throughout their time in your agency, and drop any race/ethnic groups with less than 20 students at any point along the pipeline.</li>
</ul>
<p><strong>Ask Yourself</strong></p>
<ul>
<li>How do differences between races/ethnicities change along the pipeline when only students whoever qualifying for free or reduced price lunch are examined?</li>
</ul>
<p><strong>Analytic Technique:</strong> Calculate the proportion of first-time ninth graders that progress to each step along the education pipeline.</p>
<figure id="fig-5">
<a href="pipeline_img/5.png"><img alt="pipeline_img/5.png" src="pipeline_img/5.png"/></a>
</figure>
<pre id="stlog-5" class="stcmd"><code>
<span class="stcmt">// Progression by Student Race/Ethnicity, Among FRPL Students</span>

<span class="stcmt">// Step 1: Load the college-going analysis file into Stata</span>
use "$data/college_going_analysis", clear

<span class="stcmt">// Step 2: Keep students in ninth grade cohorts you can observe persisting to the second year of college AND are ever FRPL-eligible</span>
local chrt_ninth_begin = ${chrt_ninth_begin_persist_yr2}
local chrt_ninth_end = ${chrt_ninth_end_persist_yr2}
keep if (chrt_ninth &gt;= `chrt_ninth_begin' &amp; chrt_ninth &lt;= `chrt_ninth_end')
keep if frpl_ever == 1

<span class="stcmt">// Next, repeat steps 3-9 from the previous analysis</span>
<span class="stcmt">// Step 3: Create variables for the outcomes "regular diploma recipients", "seamless transitioners" and "second year persisters" .</span>
gen grad = (!mi(chrt_grad) &amp; ontime_grad == 1)
gen seamless_transitioners_any = (enrl_1oct_ninth_yr1_any == 1 &amp; ontime_grad == 1)
gen second_year_persisters = (enrl_1oct_ninth_yr1_any == 1 &amp; enrl_1oct_ninth_yr2_any == 1 &amp; ontime_grad == 1)

<span class="stcmt">// Step 4: Create average outcomes by race/ethnicity and drop any race/ethnic groups with fewer than 20 students</span>
collapse (mean) grad seamless_transitioners_any second_year_persisters (count) N=sid, by(race_ethnicity)
drop if N &lt; 20

<span class="stcmt">// Step 5: Format the outcome variables so they read as percentages in the graph</span>
foreach var of varlist grad seamless_transitioners_any second_year_persisters {
    replace `var' = (`var' * 100)
    format `var' %9.1f
}

<span class="stcmt">// Step 6: Reformat the data file so that one variable contains all the outcomes of interest</span>
<span class="stcmt">// 1. Create 4 observations for each school: ninth grade, hs graduation, seamless college transition and second-year persistence</span>
foreach i of numlist 1/4 {
    gen time`i' = `i'
}

<span class="stcmt">// 2. Keep only African American, Asian American, Hispanic, and White students</span>
keep if race_ethnicity == 1 | race_ethnicity == 2 | race_ethnicity == 3 | race_ethnicity == 5
sort race_ethnicity
gen sortorder = _n

<span class="stcmt">// 3. Reshape the data file from wide to long</span>
reshape long time , i(sortorder)

<span class="stcmt">// 4. Create a single variable that takes on all the outcomes of interest</span>
bysort race_ethnicity: gen outcome = 100 if time == 1
bysort race_ethnicity: replace outcome = grad if time == 2
bysort race_ethnicity: replace outcome = seamless_transitioners_any if time == 3
bysort race_ethnicity: replace outcome = second_year_persisters if time == 4
format outcome %9.1f

<span class="stcmt">// Step 7: Prepare to graph the results</span>
<span class="stcmt">// 1. Label the outcome</span>
label define outcome 1 "Ninth Graders" 2 "On-time Graduates" <span class="stcmt">///</span>
3 "Seamless College Transitioners" 4 "Second Year Persisters"
label values time outcome

<span class="stcmt">// 2. Generate a cohort label to be used in the footnote for the graph</span>
local temp_begin = `chrt_ninth_begin'-1
local temp_end = `chrt_ninth_end'-1
if `chrt_ninth_begin'==`chrt_ninth_end' {
        local chrt_label "`temp_begin'-`chrt_ninth_begin'"
}
else {
        local chrt_label "`temp_begin'-`chrt_ninth_begin' through `temp_end'-`chrt_ninth_end'"
}

<span class="stcmt">// Step 8: Graph the results</span>
#delimit ;
twoway (connected outcome time if race_ethnicity==1 , sort lcolor(dknavy) mlabel(outcome)
        mlabc(black) mlabs(vsmall) mlabp(12) mcolor(dknavy) msymbol(circle) msize(small))
        (connected outcome time if race_ethnicity==3 , sort lcolor(forest_green) lpattern(dash)
        mlabel(outcome) mlabs(vsmall) mlabp(12) mlabc(black) mcolor(forest_green) msize(small))
        (connected outcome time if race_ethnicity==5 , sort lcolor(orange) mlabel(outcome) mlabc(black)
        mlabs(vsmall) mlabp(12) mcolor(orange) msymbol(circle) msize(small)),
title("Student Progression from Ninth Grade Through College", size(medium))
        subtitle("Among Students Qualifying for Free or Reduced Price Lunch" "By Student Race/Ethnicity", size(medsmall))
        xscale(range(.8(.2)4.2))
        xlabel(1 2 3 4, valuelabels labsize(vsmall))
        ytitle("Percent of Ninth Graders")
        yscale(range(0(20)100))
        ylabel(0(20)100, nogrid)
        xtitle("", color(white))
legend(order(3 1 2) col(1) position(2) size(vsmall)
        label(1 "African American Students")
        label(2 "Hispanic Students")
        label(3 "White Students")
        ring(0) region(lpattern(none) lcolor(none) fcolor(none)))
graphregion(color(white) fcolor(white) lcolor(white))
plotregion(color(white) fcolor(white) lcolor(white))
note(" " "Sample: `chrt_label' ${agency_name} first-time ninth graders." "Postsecondary enrollment outcomes from NSC matched records. All other data from ${agency_name} administrative records." , size(vsmall));
#delimit cr

graph export "$figures/A3_Progression_by_RaceEthnicity_Frpl.png", replace width(1600) height(1200)
</code></pre>
<h3 id="h-1-2-4">
Progression by Students’ On-Track Status After Ninth Grade
</h3>
<p><strong>Purpose:</strong> This analysis tracks the percent of ninth graders at different levels of being on-track for graduation who complete high school on-time, seamlessly enroll in college, and then persist to the second year of college.</p>
<p><strong>Required Analysis File Variables:</strong></p>
<ul>
<li><code>sid</code></li>
<li><code>chrt_ninth</code></li>
<li><code>ontrack_sample</code></li>
<li><code>ontrack_end_yr*</code></li>
<li><code>cum_gpa_yr*</code></li>
<li><code>ontime_grad</code></li>
<li><code>enrl_1oct_ninth_yr1_any</code></li>
<li><code>enrl_1oct_ninth_yr2_any</code></li>
</ul>
<p><strong>Analysis-Specific Sample Restrictions:</strong></p>
<ul>
<li>Only include the three most recent ninth grade cohorts for which persistence to second year of college can be reported.</li>
<li>Restrict the sample to include only students in the on-track analytic sample (students who attended the first semester of ninth grade in the system and never transferred into, or out of the system).</li>
<li>Students that obtain Special Education diplomas upon high school entry should be excluded from the analytic sample if these students are not required to meet the same graduation requirements as general education students, and if the designation can be made.</li>
</ul>
<p><strong>Ask Yourself</strong></p>
<ul>
<li>How does being on-track for graduation after ninth grade relate to on-time graduation, seamless enrollment, and second year persistence?</li>
<li>How does being on-track after ninth grade with a higher GPA compare to being on-track with a lower GPA?</li>
</ul>
<p><strong>Analytic Technique:</strong> Calculate the proportion of first-time ninth graders that progressed along the education pipeline.</p>
<figure id="fig-6">
<a href="pipeline_img/6.png"><img alt="pipeline_img/6.png" src="pipeline_img/6.png"/></a>
</figure>
<pre id="stlog-6" class="stcmd"><code>
<span class="stcmt">// Progression by Students' On-Track Status After Ninth Grade</span>

<span class="stcmt">// Step 1: Load the college-going analysis file into Stata</span>
use "$data/college_going_analysis", clear

<span class="stcmt">// Step 2: Keep students in ninth grade cohorts you can observe persisting to the second year of college AND are included in the on-track analysis sample</span>
local chrt_ninth_begin = ${chrt_ninth_begin_persist_yr2}
local chrt_ninth_end = ${chrt_ninth_end_persist_yr2}
keep if (chrt_ninth &gt;= `chrt_ninth_begin' &amp; chrt_ninth &lt;= `chrt_ninth_end')
keep if ontrack_sample == 1

<span class="stcmt">// Step 3: Generate on-track indicators that take into account studentsҠGPAs upon completion of their first year in high school</span>
label define ot 1 "Off-Track to Graduate" <span class="stcmt">///</span>
2 "On-Track to Graduate, GPA &lt; 3.0" <span class="stcmt">///</span>
3 "On-Track to Graduate, GPA &gt;= 3.0", replace

gen ontrack_endyr1_gpa = .
replace ontrack_endyr1_gpa = 1 if ontrack_endyr1 == 1
replace ontrack_endyr1_gpa = 2 if ontrack_endyr1 == 2 &amp; cum_gpa_yr1 &lt; 3 &amp; !mi(cum_gpa_yr1)
replace ontrack_endyr1_gpa = 3 if ontrack_endyr1 == 2 &amp; cum_gpa_yr1 &gt;= 3 &amp; !mi(cum_gpa_yr1)

assert !mi(ontrack_endyr1_gpa) if !mi(ontrack_endyr1) &amp; !mi(cum_gpa_yr1)
label values ontrack_endyr1_gpa ot

<span class="stcmt">// Step 4: Create variables for the outcomes "regular diploma recipients", "seamless transitioners" and "second year persisters"</span>
gen grad = (!mi(chrt_grad) &amp; ontime_grad == 1)
gen seamless_transitioners_any = (enrl_1oct_ninth_yr1_any == 1 &amp; ontime_grad == 1)
gen second_year_persisters = (enrl_1oct_ninth_yr1_any == 1 &amp; enrl_1oct_ninth_yr2_any == 1 &amp; ontime_grad == 1)

<span class="stcmt">// Step 5: Create average outcomes by on-track status at the end of ninth grade</span>
collapse (mean) grad seamless_transitioners_any second_year_persisters (count) N=sid, <span class="stcmt">///</span>
by(ontrack_endyr1_gpa)

<span class="stcmt">// Step 6: Format the outcome variables so they read as percentages in the graph</span>
foreach var of varlist grad seamless_transitioners_any second_year_persisters {
    replace `var' = (`var' * 100)
    format `var' %9.1f
}

<span class="stcmt">// Step 7: Reformat the data file so that one variable contains all the outcomes of interest</span>
<span class="stcmt">// 1. Create 4 observations for each school: ninth grade, hs graduation, seamless college transition and second-year persistence</span>
foreach i of numlist 1/4 {
    gen time`i' = `i'
}

<span class="stcmt">// 2. Reshape the data file from wide to long</span>
reshape long time, i(ontrack_endyr1_gpa N)

<span class="stcmt">// 3. Create a single variable that takes on all the outcomes of interest</span>
bysort ontrack_endyr1_gpa: gen outcome = 100 if time == 1
bysort ontrack_endyr1_gpa: replace outcome = grad if time == 2
bysort ontrack_endyr1_gpa: replace outcome = seamless_transitioners_any if time == 3
bysort ontrack_endyr1_gpa: replace outcome = second_year_persisters if time == 4
format outcome %9.1f

<span class="stcmt">// Step 8: Prepare to graph the results</span>
<span class="stcmt">// 1. Label the outcome</span>
label define outcome 1 "Ninth Graders" 2 "On-time Graduates" <span class="stcmt">///</span>
3 "Seamless College Transitioners" 4 "Second Year Persisters"
label values time outcome

<span class="stcmt">// 2. Generate a cohort label to be used in the footnote for the graph</span>
local temp_begin = `chrt_ninth_begin'-1
local temp_end = `chrt_ninth_end'-1
if `chrt_ninth_begin'==`chrt_ninth_end' {
        local chrt_label "`temp_begin'-`chrt_ninth_begin'"
}
else {
        local chrt_label "`temp_begin'-`chrt_ninth_begin' through `temp_end'-`chrt_ninth_end'"
}

<span class="stcmt">// Step 9: Graph the results</span>
#delimit ;
twoway (connected outcome time if ontrack_endyr1_gpa == 1,
        sort lcolor(dkorange) mlabel(outcome) mlabc(black) mlabs(vsmall) mlabp(3)
        mcolor(dkorange) msymbol(circle) msize(small))
        (connected outcome time if ontrack_endyr1_gpa == 2, sort lcolor(navy*.6)
        mlabel(outcome) mlabs(vsmall) mlabp(3) mlabc(black) mcolor(navy*.6)
        msymbol(square) msize(small))
        (connected outcome time if ontrack_endyr1_gpa == 3, sort lcolor(navy*.9)
        mlabel(outcome) mlabs(vsmall) mlabp(3) mlabc(black) mcolor(navy*.9)
        msymbol(diamond) msize(small))
        (connected outcome time if ontrack_endyr1_gpa == 4, sort lcolor(navy*.3)
        mlabel(outcome) mlabs(vsmall) mlabp(3) mlabc(black) mcolor(navy*.3)
        msymbol(triangle) msize(small)),
title("Student Progression from Ninth Grade Through College", size(medium))
        ylabel(, nogrid)
        subtitle("By Course Credits and GPA after First High School Year", size(medsmall))
        xscale(range(.8(.2)4.2)) xlabel(1 2 3 4, valuelabels labsize(vsmall)) xtitle("")
        yscale(range(0(20)100)) ylabel(0(20)100, labsize(small) format(%9.0f))
        ytitle("Percent of Ninth Graders" " ")
legend(order(3 2 1) col(1) position(1) size(vsmall)
        label(1 "Off-Track to Graduate")
        label(2 "On-Track to Graduate, GPA&lt;3.0")
        label(3 "On-Track to Graduate, GPA&gt;=3.0")
        ring(0) region(lpattern(none) lcolor(none) fcolor(none)))
graphregion(color(white) fcolor(white) lcolor(white))
plotregion(color(white) fcolor(white) lcolor(white))
note(" " "Sample: `chrt_label' ${agency_name} first-time ninth graders. Students who transferred into or out of ${agency_name} are excluded" "from the sample. Postsecondary enrollment outcomes from NSC matched records. All other data are from ${agency_name} administrative records.", span size(vsmall));
#delimit cr

graph export "$figures/A4_Progression_by_OnTrack_Ninth.png", replace width(1600) height(1200)


</code></pre>
<h4 id="this-guide-was-originally-created-by-the-strategic-data-project."><em>This guide was originally created by the Strategic Data Project.</em></h4>
</div>
</div>
</div>
<script src="https://code.jquery.com/jquery-1.12.4.min.js" integrity="sha256-ZosEbRLbNQzLpnKIkEdrPv7lOy9C27hHQ+Xp8a4MxAQ=" crossorigin="anonymous"></script>
<script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/js/bootstrap.min.js" integrity="sha384-Tc5IQib027qvyjSMfHjOMaLkfuWVxZxUPnCJA7l2mCWNIpG9mGCD8wGNIcPD7Txa" crossorigin="anonymous"></script>
</body>
</html>
</body>
</html>
