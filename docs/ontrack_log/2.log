
<span class="stcmt">// Proportion of Students On-Track by High School</span>

<span class="stcmt">// Step 1: Load the college-going analysis file into Stata</span>
use "$data/college_going_analysis", clear
 
<span class="stcmt">// Step 2: Keep students in ninth grade cohorts you can observe graduating high school on time AND are part of the on-track sample</span>
local chrt_ninth_begin = ${chrt_ninth_begin_grad}
local chrt_ninth_end = ${chrt_ninth_end_grad}
keep if (chrt_ninth &gt;= `chrt_ninth_begin' &amp; chrt_ninth &lt;= `chrt_ninth_end')
keep if ontrack_sample == 1
 
<span class="stcmt">// Step 3: Create on-track categories that account for students“†credits earned (already captured in the ontrack_endyr1 variable) and GPA after ninth grade</span>
gen ontrack_endyr1_1 = 0
replace ontrack_endyr1_1 = 1 if ontrack_endyr1 == 1
label var ontrack_endyr1_1 "Off-Track to Graduate"

gen ontrack_endyr1_2 = 0
replace ontrack_endyr1_2 = 1 if ontrack_endyr1 == 2 &amp; cum_gpa_yr1 &lt; 3.0
label var ontrack_endyr1_2 "On-Track to Graduate, GPA &lt; 3.0"

gen ontrack_endyr1_3 = 0
replace ontrack_endyr1_3 = 1 if ontrack_endyr1 == 2 &amp; cum_gpa_yr1 &gt;= 3.0 &amp; <span class="stcmt">///</span>
!mi(cum_gpa_yr1)
label var ontrack_endyr1_3 "On-Track to Graduate, GPA &gt;= 3.0"

assert ontrack_endyr1_1 + ontrack_endyr1_2 + ontrack_endyr1_3 == 1
 
<span class="stcmt">// Step 4: Obtain the agency average for the key variables</span>
preserve
	collapse (mean) ontrack_endyr1_? (count) N=sid
	tempfile agency_level
	save `agency_level'       
restore
 
<span class="stcmt">// Step 5: Obtain mean rates for each school and append the agency average</span>
collapse (mean) ontrack_endyr1_? (count) N=sid, by(first_hs_name first_hs_code)
append using `agency_level'
 
<span class="stcmt">// Step 6: Provide a hs name label for the appended agency average and shorten hs name</span>
replace first_hs_code = 0 if first_hs_code == .
replace first_hs_name = "${agency_name} AVERAGE" if mi(first_hs_name)
replace first_hs_name = subinstr(first_hs_name, " High School", "", .)
 
<span class="stcmt">// Step 7: For students who are off-track upon completion of their first year of high school, convert the values to be negative for ease of visualization in the graph</span>
replace ontrack_endyr1_1 = -ontrack_endyr1_1
 
<span class="stcmt">// Step 8: Multiply the average of each outcome by 100 for graphical representation of the rates. Create a variable equal to the sum of the two on-track status variables for easier sorting</span>
foreach var of varlist ontrack_endyr1_1 ontrack_endyr1_2 ontrack_endyr1_3  {
	replace `var' = (`var' * 100)
	format `var' %9.1f
}
gen ontrack_endyr1_sum = ontrack_endyr1_2 + ontrack_endyr1_3
 
<span class="stcmt">// Step 9: Prepare to graph the results</span>
<span class="stcmt">// Generate a cohort label to be used in the footnote for the graph</span>
local temp_begin = `chrt_ninth_begin'-1
local temp_end = `chrt_ninth_end'-1
if `chrt_ninth_begin'==`chrt_ninth_end' {
    local chrt_label "`temp_begin'-`chrt_ninth_begin'"
} 
else {
    local chrt_label "`temp_begin'-`chrt_ninth_begin' through `temp_end'-`chrt_ninth_end'"
}
 
<span class="stcmt">// Step 10: Graph the results</span>
#delimit ;
graph bar ontrack_endyr1_3 ontrack_endyr1_2 ontrack_endyr1_1,
    over(first_hs_name, gap(20) sort(ontrack_endyr1_sum) label(angle(40)labsize(small)))
    blabel(bar, position(inside) size(2) format(%8.0f))
    bar(3, fcolor(maroon*.6) lcolor(maroon*.6))
    bar(1, fcolor(navy*.5) lcolor(navy*.5))
    bar(2, fcolor(navy*.8) lcolor(navy*.8)) stack
title("Proportion of Students On-Track to Graduate by School", size(medium))
    subtitle("End of Ninth Grade On-Track Status" "By High School")
legend(region(lcolor(white)) position(11) ring(0) order(2 1 3)
    label(3 "Off Track to Graduate")
    label(1 "On Track, GPA &lt;3.0")
    label(2 "On Track, GPA&gt;=3.0")
    symxsize(5) symysize(2) cols(1) size(vsmall))
yline(0, lcolor(black) lwidth(vvthin))
ytitle("Percent of Ninth Graders") yscale(range(-80(20)80)) ylabel(-80(20)80, nogrid)
graphregion(color(white) fcolor(white) lcolor(white))
plotregion(color(white) fcolor(white) lcolor(white))
note(" " "Sample: `chrt_label' ${agency_name} first-time ninth graders. Students who transferred into or out of ${agency_name} are excluded" "from the sample. All data from ${agency_name} administrative records.", span size(vsmall));
#delimit cr

graph export "figures/B1_OnTrack_Ninth_by_HS.png", replace width(1600) height(1200)

